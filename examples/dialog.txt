PROC main:
	LOCAL choice%, dlg%
	dINIT "Dialog test", 0
	dTEXT "", "Pick a dialog type to test"
	REM dTEXT "", "", $800
	dCHOICE choice%, "Choose", "long,edit,checkbox,text,password,empty,date,time,buttons,cancelDlg"
	dBUTTONS "OK", 13, "Cancel", -(512 + %q)
	choice% = 2
	dlg% = DIALOG
	PRINT "Dialog returned ", dlg%
	PRINT "Choice was ", choice%
	IF dlg% = 0
		PRINT "(dialog cancelled)"
	ELSEIF choice% = 1
		longDlg:
	ELSEIF choice% = 2
		editDlg:
	ELSEIF choice% = 3
		checkDlg:
	ELSEIF choice% = 4
		textDlg:
	ELSEIF choice% = 5
		xDlg:
	ELSEIF choice% = 6
		emptyDlg:
	ELSEIF choice% = 7
		dateDlg:
	ELSEIF choice% = 8
		timeDlg:
	ELSEIF choice% = 9
		buttonDlg:
	ELSEIF choice% = 10
		cancelDlg:
	ENDIF
ENDP

PROC buttonDlg:
	LOCAL result%
	dINIT "Buttons"
	dBUTTONS "OK", 13, "Cancel", -27, "A", %a, "B", %b, "C", -%c
	result% = DIALOG
	IF result% = 13:
		PRINT "OK"
	ELSEIF result% = %a
		PRINT "A"
	ELSEIF result% = %b
		PRINT "B"
	ELSEIF result% = 0
		PRINT "Dialog Cancelled (Esc or Ctrl+C Pressed)"
	ENDIF
	GET
ENDP

PROC longDlg:
	LOCAL val&
	dINIT "Long input"
	dLONG val&, "Long", -1, 5
	dBUTTONS "OK", 13
	DIALOG
	GET
ENDP

PROC editDlg:
	LOCAL val$(16)
	dINIT "Text edit"
	dEDIT val$, "Text"
	dBUTTONS "OK", 13
	DIALOG
	GET
ENDP

PROC checkDlg:
	LOCAL chk%
	dINIT "Checkbox"
	dCHECKBOX chk%, "check"
	DIALOG
	GET
ENDP

PROC textDlg:
	LOCAL x%
	dINIT "Text test"
	dTEXT "", "Right aligned linebelow", $601
	dTEXT "", "Full width text that isn't selectable"
	dTEXT "", "", $800
	dTEXT "Prompt", "Body text", $600
	dTEXT "Another", "Option", $400
	x% = DIALOG
	PRINT "Selected", x%
	GET
ENDP

PROC xDlg:
	LOCAL x$(8)
	x$ = "woop"
	dINIT "Password test"
	dXINPUT x$, "Pass"
	DIALOG
	PRINT "Entered:", x$
	GET
ENDP

PROC emptyDlg:
	dINIT "No items dialog"
	PRINT DIALOG
	GET
ENDP

PROC dateDlg:
	LOCAL x&
	x& = DAYS(DAY, MONTH, YEAR)
	dINIT "Date test"
	dDATE x&, "Date", x& - 1, x& + 6
	DIALOG
	PRINT "Entered:", x&
	GET
ENDP

PROC timeDlg:
	LOCAL x&
	x& = HOUR * INT(3600) + MINUTE * 60 + SECOND
	dINIT "Time test"
	dTIME x&, "Time", 1, 0, &1517F
	DIALOG
	PRINT "Entered:", x&
	GET
ENDP

PROC cancelDlg:
	LOCAL result%
	dINIT "Doom"
	dTEXT "", "EVERYTHING IS DOOMED"
	dBUTTONS "Sob", -27
	result% = DIALOG
	PRINT "Dialog returned", result%
	GET
ENDP
