PROC main:
	GLOBAL choice%
	LOCAL cont%
	cont% = 1
	WHILE cont%
		cont% = loop%:
	ENDWH
ENDP

PROC loop%:
	LOCAL dlg%	
	dINIT "Dialog test", 0
	dTEXT "", "Pick a dialog type to test"
	REM dTEXT "", "", $800
	dCHOICE choice%, "Choose", "long,float,edit,checkbox,text,..."
	dCHOICE choice%, "", "password,empty,date,time,..."
	dCHOICE choice%, "", "buttons,bareButtons,cancelDlg,..."
	dCHOICE choice%, "", "editmulti"
	dBUTTONS "OK", 13, "Cancel", -(512 + %q)
	choice% = 2
	dlg% = DIALOG
	PRINT "Dialog returned ", dlg%
	PRINT "Choice was ", choice%
	IF dlg% = 0
		PRINT "(dialog cancelled)"
		GET
		RETURN 0
	ELSEIF choice% = 1
		longDlg:
	ELSEIF choice% = 2
		floatDlg:
	ELSEIF choice% = 3
		editDlg:
	ELSEIF choice% = 4
		checkDlg:
	ELSEIF choice% = 5
		textDlg:
	ELSEIF choice% = 6
		xDlg:
	ELSEIF choice% = 7
		emptyDlg:
	ELSEIF choice% = 8
		dateDlg:
	ELSEIF choice% = 9
		timeDlg:
	ELSEIF choice% = 10
		buttonDlg:
	ELSEIF choice% = 11
		bareButtonDlg:
	ELSEIF choice% = 12
		cancelDlg:
	ELSEIF choice% = 13
		editMulti:
	ENDIF
	
	RETURN 1
ENDP

PROC buttonDlg:
	LOCAL result%
	dINIT "Buttons"
	dBUTTONS "OK", 13, "Cancel", -27, "Tab", 9, "A", %a, "B", -%b
	result% = DIALOG
	IF result% = 13:
		PRINT "OK"
	ELSEIF result% = 9
		PRINT "Tab"
	ELSEIF result% = %a
		PRINT "A"
	ELSEIF result% = %b
		PRINT "B"
	ELSEIF result% = 0
		PRINT "Dialog Cancelled (result = 0)"
	ENDIF
	GET
ENDP

PROC bareButtonDlg:
	LOCAL result%
	dINIT "Buttons"
	dBUTTONS "No", $34E, "Yes", $359
	result% = DIALOG
	PRINT "Dialog returned", result%
	GET
ENDP

PROC longDlg:
	LOCAL val&
	dINIT "Long input"
	dLONG val&, "Long", -1, 5
	dBUTTONS "OK", 13
	DIALOG
	PRINT "Entered:", val&
	GET
ENDP

PROC floatDlg:
	LOCAL fval
	fval = 2.0
	dINIT "Float input"
	dFLOAT fval, "Long", -1.1, 5.0
	dBUTTONS "OK", 13
	DIALOG
	PRINT "Entered:", fval
	GET
ENDP

PROC editDlg:
	LOCAL val$(16)
	dINIT "Text edit"
	dEDIT val$, "Text"
	dBUTTONS "OK", 13
	DIALOG
	PRINT "Got >", val$, "<"
	GET
ENDP

PROC checkDlg:
	LOCAL chk%
	dINIT "Checkbox"
	dCHECKBOX chk%, "check"
	DIALOG
	PRINT "check is", chk%
	GET
ENDP

PROC textDlg:
	LOCAL x%
	dINIT "Text test"
	dTEXT "", "Right aligned linebelow", $601
	dTEXT "", "Full width text that isn't selectable"
	dTEXT "", "", $800
	dTEXT "Prompt", "Body text", $600
	dTEXT "Another", "Option", $400
	x% = DIALOG
	PRINT "Selected", x%
	GET
ENDP

PROC xDlg:
	LOCAL x$(20)
	x$ = "12345"
	dINIT "Password test"
	dXINPUT x$, "Pass"
	DIALOG
	PRINT "Entered:", x$
	GET
ENDP

PROC emptyDlg:
	dINIT "No items dialog"
	PRINT DIALOG
	GET
ENDP

PROC dateDlg:
	LOCAL x&
	x& = DAYS(DAY, MONTH, YEAR)
	dINIT "Date test"
	dDATE x&, "Date", x& - 1, x& + 6
	DIALOG
	PRINT "Entered:", x&
	GET
ENDP

PROC timeDlg:
	LOCAL thms&, thm&, thms24&, dhm&, dhms&
	thms& = HOUR * INT(3600) + MINUTE * 60 + SECOND
	thm& = thms&
	thms24& = thms&
	dhm& = 65
	dhms& = 65
	dINIT "Time test"
	dTIME thm&, "Time H:M", 0, 0, &1517F
	dTIME thms&, "Time H:M:S", 1, 0, &1517F
	dTIME thms24&, "Time H:M:S 24", 9, 0, &1517F
	dTIME dhm&, "Duration H:M", 2, 0, 600
	dTIME dhms&, "Duration H:M:S", 3, 0, 600
	REM dTIME dms&, "Duration M:S", 7, 0, 6000
	DIALOG
	PRINT "Entered:", thm&, thms&, thms24&, dhm&, dhms&
	GET
ENDP

PROC cancelDlg:
	LOCAL result%
	dINIT "Doom"
	dTEXT "", "EVERYTHING IS DOOMED"
	dBUTTONS "Sob", 27
	result% = DIALOG
	PRINT "Dialog returned", result%
	GET
ENDP

PROC editMulti:
	LOCAL result%, buf&(16), ptr&, text$(60), len&
	ptr& = ADDR(buf&())
	dINIT "Multi edit"
	dEDITMULTI ptr&, "Prompt", 20, 5, 60
	dBUTTONS "OK", %o
	result% = DIALOG
	len& = PEEKL(ptr&)
	POKEB ptr& + 3, len&
	text$ = PEEK$(ptr& + 3)
	PRINT "Dialog returned", text$
	GET
ENDP

